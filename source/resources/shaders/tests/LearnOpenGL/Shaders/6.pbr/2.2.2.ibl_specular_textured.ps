//2.2.2.ibl_specular_textured.ps
#version 330 core
out vec4 FragColor;

in VS_OUT{
	vec3 FragPos;
	vec3 Normal;
	vec2 TexCoords;
} ps_in;

// material parameters
uniform sampler2D albedoMap;	// 反照率贴图
uniform sampler2D normalMap;	// 法线贴图
uniform sampler2D metallicMap;	// 金属度
uniform sampler2D roughnessMap;	// 粗糙度
uniform sampler2D aoMap;		// 环境遮蔽

// IBL
uniform samplerCube irradianceMap;
uniform samplerCube prefilterMap;
uniform sampler2D brdfLUT;

// lights 
uniform vec3 lightPositions[4];
uniform vec3 lightColors[4];

uniform vec3 camPos;

const float PI = 3.14159265359;


/*
	Easy trick to get tangent-normals to world-space to keep PBR code simplified.
	Don't worry if you don't get what's going on; you generally want to do normal
	mapping the usal way for performance anways; I do plan make a note of this
	technique somewhere later in the normal mapping tutorial.
*/
vec3 getNormalFromMap()
{
	vec3 tangentNormal = texture(normalMap, ps_in.TexCoords).xyz * 2.0 - 1.0; // 将法线从[0,1]变换到[-1，1]
	vec3 Q1 = dFdx(ps_in.FragPos);
	vec3 Q2 = dFdy(ps_in.FragPos);
	vec2 st1 = dFdx(ps_in.TexCoords);
	vec2 st2 = dFdy(ps_in.TexCoords);

	vec3 N = normalize(ps_in.Normal);
	vec3 T = normalize(Q1 * st2.t - Q2 * st1.t);
	vec3 B = -normalize(cross(N, T));
	mat3 TBN = mat3(T, B, N);
	return normalize(TBN * tangentNormal);
}

// ----------------------------------------------------------------------------
// 正态分布函数

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
	float a = roughness*roughness;
	float a2 = a*a;
	float NdotH = max(dot(N, H), 0.0);
	float NdotH2 = NdotH*NdotH;

	float nom = a2;
	float denom = (NdotH2 * (a2 - 1.0) + 1.0);
	denom = PI * denom * denom;

	return nom / denom;
}

// Schlick-GGX： sclick几何函数和GGX函数的合体函数，
float GeometrySchlickGGX(float NdotV, float roughness)
{
	float r = (roughness + 1.0);
	float k = (r * r) / 8.0;

	float nom = NdotV;
	float denom = NdotV * (1.0 - k) + k;

	return nom / denom;

}

// 史密斯法
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
	float NdotV = max(dot(N, V), 0.0);
	float NdotL = max(dot(N, L), 0.0);
	float ggx2 = GeometrySchlickGGX(NdotV, roughness);
	float ggx1 = GeometrySchlickGGX(NdotL, roughness);

	return ggx1 * ggx2;
}

// 菲涅尔方程
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
	return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

// 菲涅耳-表面的粗糙度
vec3 fresnelSchlickRoughness(float cosTheta, vec3 F0, float roughness)
{
	return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(1.0 - cosTheta, 5.0);
}


void main()
{
	// material properties
	vec3 albedo = pow(texture(albedoMap, ps_in.TexCoords).rgb, vec3(2.2));
	float metallic = texture(metallicMap, ps_in.TexCoords).r;
	float roughness = texture(roughnessMap, ps_in.TexCoords).r;
	float ao = texture(aoMap, ps_in.TexCoords).r;

	// input lighting data
	vec3 N = getNormalFromMap();
	vec3 V = normalize(camPos - ps_in.FragPos);
	vec3 R = reflect(-V, N);

	// calculate reflectance at normal incidence; if dia-electric(lick plastic) use F0
	// of 0.04 and if it's a metal, use their albedo color as F0(metallic workflow)
	vec3 F0 = vec3(0.04);
	F0 = mix(F0, albedo, metallic);

	// reflectance equation
	vec3 Lo = vec3(0.0);
	for (int i = 0; i < 4; ++i)
	{
		// calculate per-light radiance
		vec3 L = normalize(lightPositions[i] - ps_in.FragPos);
		vec3 H = normalize(V + L);
		float distance = length(lightPositions[i] - ps_in.FragPos);
		float attenuation = 1.0 / (distance * distance);
		vec3 radiance = lightColors[i] * attenuation;

		// Cook-Torrance BRDF
		float NDF = DistributionGGX(N, H, roughness);
		float G = GeometrySmith(N, V, L, roughness);
		vec3 F = fresnelSchlick(max(dot(H, V), 0.0), F0);
				 
		vec3 nominator = NDF * G * F;
		// 0.001 to prevent divide  by zero
		float denominator = 4 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001;
		vec3 specular = nominator / denominator;

		// kS is equal to Fresnel
		vec3 kS = F;
		// for energy conservation, the diffuse and specular light can't
		// be above 1.0(unless the surface emits light); to preserve this
		// releationship the diffuse component(kD) should equal 1.0 - kS.
		vec3 kD = vec3(1.0) - kS;
		// multiply kD by the inverse metalness such that only non-metals
		// have diffuse lighting, or a linear blend if partly metal(pure metals
		// have no diffuse light).
		kD *= 1.0 - metallic;

		// scale light by NdotL
		float NdotL = max(dot(N, L), 0.0);

		// add to outgoing radiance Lo
		// note that we already multiplied the BRDF by the  by the Fresnel (kS) so we won't multiply by kS again
		Lo += (kD * albedo / PI + specular) * radiance * NdotL;
	}

	// ambient lighting(we now use IBL as the ambient term)
	vec3 F = fresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);

	vec3 kS = F;
	vec3 kD = 1.0 - kS;
	kD *= 1.0 - metallic;

	vec3 irradiance = texture(irradianceMap, N).rgb;
	vec3 diffuse = irradiance * albedo;

	// sample both the pre-filter map and the BRDF lut and combine them together as 
	// per the Split-Sum approximation to get the IBL specular part.
	const float MAX_REFLECTION_LOD = 4.0;
	vec3 prefilteredColor = textureLod(prefilterMap, R, roughness * MAX_REFLECTION_LOD).rgb;
	vec2 brdf = texture(brdfLUT, vec2(max(dot(N, V), 0.0), roughness)).rg;
	vec3 specular = prefilteredColor * (F * brdf.x + brdf.y);

	vec3 ambient = (kD * diffuse + specular) * ao;

	vec3 color = ambient + Lo;

	// HDR tonemapping
	color = color / (color + vec3(1.0));
	// gamma correct
	color = pow(color, vec3(1.0 / 2.2));

	FragColor = vec4(color, 1.0);

}
